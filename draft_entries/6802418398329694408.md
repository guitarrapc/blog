---
Title: TertraformでStorage LensをFree metricsで展開する
Category:
- Terraform
- AWS
EditURL: https://blog.hatena.ne.jp/guitarrapc_tech/guitarrapc-tech.hatenablog.com/atom/entry/6802418398329694408
PreviewURL: https://tech.guitarrapc.com/draft/entry/_Tz_2lf1k3JoobhSwe65HbUoI6Y
CustomPath: 2025/02/15/235900
Draft: true
---

S3のStorage LensはFree metricsだけでも割と便利なので、Terraformで展開したくなるのですがやったことなかったのでメモです。
うっかり展開するとすぐにAdvanced metricsが有効になるので罠っぽさありました。

[:contents]

# Storage LensをFree metricsで展開する

以下のように`account_level`と`bucket_level`を両方空にするとFree metricsになります。

```terraform
resource "aws_s3control_storage_lens_configuration" "main" {
  config_id = "default"

  storage_lens_configuration {
    enabled = true

    # 無指定だとFree metricsになる
    account_level {
      bucket_level {
      }
    }

    data_export {
      cloud_watch_metrics {
        enabled = false
      }
    }
  }
}
```

![image](https://github.com/user-attachments/assets/72558c86-09ef-4bf6-9f3a-0d95a3b8a171)

## 失敗例

以下のように`account_level`と`bucket_level`を指定するとAdvanced metricsになります。全部falseでもAdvanced metricsになるの罠すぎる。

```terraform
resource "aws_s3control_storage_lens_configuration" "main" {
  config_id = var.storage_lens_name

  storage_lens_configuration {
    enabled = true

    # 全部falseでもAdvanced metricsになる
    account_level {
      activity_metrics { enabled = false }
      advanced_cost_optimization_metrics { enabled = false }
      advanced_data_protection_metrics { enabled = false }
      detailed_status_code_metrics { enabled = false }

      bucket_level {
        activity_metrics { enabled = false }
        advanced_cost_optimization_metrics { enabled = false }
        advanced_data_protection_metrics { enabled = false }
        detailed_status_code_metrics { enabled = false }
      }
    }

    data_export {
      cloud_watch_metrics {
        enabled = false
      }
    }
  }
}
```

差分は以下の通りです。

```
  ~ resource "aws_s3control_storage_lens_configuration" "main" {
        id         = "123456789012:default"
        tags       = {}
        # (4 unchanged attributes hidden)
      ~ storage_lens_configuration {
            # (1 unchanged attribute hidden)
          ~ account_level {
              + activity_metrics {
                  + enabled = false
                }
              + advanced_cost_optimization_metrics {
                  + enabled = false
                }
              + advanced_data_protection_metrics {
                  + enabled = false
                }
              ~ bucket_level {
                  + activity_metrics {
                      + enabled = false
                    }
                  + advanced_cost_optimization_metrics {
                      + enabled = false
                    }
                  + advanced_data_protection_metrics {
                      + enabled = false
                    }
                  + detailed_status_code_metrics {
                      + enabled = false
                    }
                }
              + detailed_status_code_metrics {
                  + enabled = false
                }
            }
            # (1 unchanged block hidden)
        }
    }
```

Advancedになるの悲しい。

![image](https://github.com/user-attachments/assets/c0ce0002-1e28-4907-9961-1053ec87c191)


# まとめ

もともとはGlacierの利用分ってStorage Lensで見られたかな?というのが始まりだったのですが、そういえばStorage Lensではみれないのでした。残念。
