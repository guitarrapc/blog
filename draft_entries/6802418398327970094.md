---
Title: TerraformのEphemeral resourceをどう使うのか
Category:
- Terraform
EditURL: https://blog.hatena.ne.jp/guitarrapc_tech/guitarrapc-tech.hatenablog.com/atom/entry/6802418398327970094
PreviewURL: https://tech.guitarrapc.com/draft/entry/wDaG4wXKzmV731mWhMQCCk2-KIg
CustomPath: 2025/02/12/235900
Draft: true
---

Terraform 1.10.0からEphemeral resourceが追加されました。これは各プロバイダーが対応するEpheemeral resourceと組み合わせて使う安全なData Sourceと呼ぶべきものです。
今回はEphemeral resourceをどのように使うのか調べたメモです。

[:contents]

# Data Sourceの問題点

Terraformの[Data Source](https://developer.hashicorp.com/terraform/language/data-sources)は、Terraformの実行時にリソースを読み取るためのものです。Data Sourceは取得した値をStateやplanファイルに保存しその値を使ってリソースを作成します。このためData Sourceを使う場合は、planファイルやStateファイルに機密情報が残ることに注意が必要です。Ephemeral Resourceの動作を見る前に、Data Sourceで取得した値がStateに書き込まれるのを確認してみましょう。

まずはSSM Parameter Storeにbarというキーで`secret-value`という値を保存します。Data Sourceで取得したいので、AWS Consoleやaws-cliでキーを作っておきます。

![image](https://github.com/user-attachments/assets/6eb5aba4-954e-4439-aa6c-91b47f9cc1eb)

次に、Data Sourceを使って値を取得するTerraformコードを書いてApplyします。Data Sourceだけの変更では`terraform apply`でStateファイルに書き込まれないので適当なリソースも追加します。

```terraform
# data sourceで取得した値はStateファイルに...?
data "aws_ssm_parameter" "main" {
  name = "bar"
}

resource "aws_ssm_parameter" "main" {
  name  = "foo"
  type  = "String"
  value = data.aws_ssm_parameter.main.value
}
```

Stateファイルで確認すると、Data Sourceで取得した値がStateファイルに書き込まれていることがわかります。残念です。

```json
{
  "module": "module.ssm",
  "mode": "data",
  "type": "aws_ssm_parameter",
  "name": "main",
  "provider": "provider[\"registry.terraform.io/hashicorp/aws\"]",
  "instances": [
    {
      "schema_version": 0,
      "attributes": {
        "arn": "arn:aws:ssm:ap-northeast-1:xxxxxxxxxxxx:parameter/bar",
        "id": "bar",
        "insecure_value": "secret-value",
        "name": "bar",
        "type": "String",
        "value": "secret-value",
        "version": 1,
        "with_decryption": true
      },
      "sensitive_attributes": [
        [
          {
            "type": "get_attr",
            "value": "value"
          }
        ]
      ]
    }
  ]
}
// resource aws_ssm_parameter のjsonは省略
```

![image](https://github.com/user-attachments/assets/fad5c746-51bc-48bd-a88b-d3088d7794c6)

# Ephemeral Resourceの挙動

[Ephemeral Resource](https://developer.hashicorp.com/terraform/language/v1.10.x/resources/ephemeral)はData Sourceと違い、Stateファイルに値を書き込みません。つまり、Terraformの実行時にリソースを取得しその値をその時だけ使う一時リソースなイメージです。「Ephemeral Resourceは固有のライフサイクルを持つ」とドキュメントにありますが、実際ライフサイクルを見ると利用用途がイメージしやすいです。まさにリソースに利用するため一時的にシークレットにアクセスして、その値を中継するというイメージです。

![image](https://github.com/user-attachments/assets/940ed5f8-2086-421e-b839-4623dee179c1)

先ほどのData Sourceの例をEphemeral Resourceを使って書き換えてみましょう。

```terraform
data "aws_region" "current" {}
data "aws_caller_identity" "current" {}

ephemeral "aws_ssm_parameter" "foo" {
  arn = "arn:aws:ssm:${data.aws_region.current.name}:${data.aws_caller_identity.current.account_id}:parameter/bar"
}
```

この内容を`terraform apply`してもStateファイルにリソースが書きこまれません。やったー。

## Ephemeral Resourceの値はResourceで使えない

ライフサイクルから考えると、従来のResourceやData SourceにEphemeral Resourceの値を使えないと予想できます。使ったらStateファイルに値が書き込まれてしまってコンセプトとずれますから当然ですね。試してみましょう。

```terraform
data "aws_region" "current" {}
data "aws_caller_identity" "current" {}

ephemeral "aws_ssm_parameter" "foo" {
  arn = "arn:aws:ssm:${data.aws_region.current.name}:${data.aws_caller_identity.current.account_id}:parameter/bar"
}

resource "aws_ssm_parameter" "main" {
  name = "foo"
  type = "String"
  value = ephemeral.aws_ssm_parameter.foo.value
}
```

期待通り、ResourceでEphemeral Resourceの値を参照できません。

```sh
$ terraform validate
╷
│ Error: Invalid use of ephemeral value
│
│   with module.ssm.aws_ssm_parameter.main,
│   on ../modules/ssm/main.tf line 16, in resource "aws_ssm_parameter" "main":
│   16:   value = ephemeral.aws_ssm_parameter.foo.value
│
│ Ephemeral values are not valid in resource arguments, because resource instances must persist between Terraform phases.
╵
```

## Ephemeral Resourceの値を参照する

Epheneral Resourceの値は、local values、ephemeral variables[^1]、ephemeral outputs[^2]、provider、provisionerとconnectionブロックで利用できます。

![image](https://github.com/user-attachments/assets/b158cdb7-4bfc-41a9-b78b-79c4fd8fffbd)

つまりプロバイダーが現実的な利用箇所ですね。[^3]プロバイダーでしか使えないのは割と使い勝手が悪いですが、まぁしょうがない。ドキュメントでPostgreSQLへのDB接続のために利用しているのは、まさに典型ということです。[^4]

```terraform
ephemeral "aws_secretsmanager_secret_version" "db_master" {
  secret_id = data.aws_db_instance.example.master_user_secret[0].secret_arn
}

locals {
  credentials = jsondecode(ephemeral.aws_secretsmanager_secret_version.db_master.secret_string)
}

provider "postgresql" {
  host     = data.aws_db_instance.example.address
  port     = data.aws_db_instance.example.port
  username = local.credentials["username"]
  password = local.credentials["password"]
}
```

私はEKSでKubernetesを使うので、[kubernetes provider](https://registry.terraform.io/providers/hashicorp/kubernetes/latest/docs)でEphemeral Resourceが使えるとうれしいです。幸い[Issue #40343](https://github.com/hashicorp/terraform-provider-aws/issues/40343)と[PR #40660](https://github.com/hashicorp/terraform-provider-aws/pull/40660)からEphemeral Resource`aws_eks_cluster_auth`が追加され[AWS Provider v5.84.0](https://github.com/hashicorp/terraform-provider-aws/releases/tag/v5.84.0)でリリースされます。確かにtokenは気になっていたのでうれしいですね。

```terraform
ephemeral "aws_eks_cluster_auth" "example" {
  name = data.aws_eks_cluster.example.id
}

provider "kubernetes" {
  host                   = data.aws_eks_cluster.example.endpoint
  cluster_ca_certificate = base64decode(data.aws_eks_cluster.example.certificate_authority[0].data)
  token                  = ephemeral.aws_eks_cluster_auth.example.token
}

provider "helm" {
  kubernetes {
    host                   = data.aws_eks_cluster.example.endpoint
    cluster_ca_certificate = base64decode(data.aws_eks_cluster.example.certificate_authority[0].data)
    token                  = ephemeral.aws_eks_cluster_auth.example.token
  }
}
```

## メモ

[Ephemeral Resourceに対応するリソース実装](https://github.com/hashicorp/terraform-provider-aws/pull/40660/files)を見ると、`terraform-plugin-framework/ephemeral`の`ephemeral.MetadataRequest`と`ephemeral.MetadataResponse`を使ってデータを取得するんですね。単純にstsClient経由で値を直接とってきて、リクエスト/レスポンスに詰めているだけって感じですが、なるほど都度通信入るのは既存のTerraform処理的にもあまり変わらないので違和感ないです。

# まとめ

Ephemeral ResourceはData Sourceと違い、Stateファイルに値を書き込まないため機密情報を扱う際に安全です。Terraform 1.10以上、かつ対応するEphemeral Resourceが追加されないといけないので、しばらくはProviderの対応待ちになりそうですが、Data SourceでStateに保存されるのはずっと疑問だったので是正されてうれしい限りです。

できればResourceのpasswordとかのセクションに関しても、Stateファイルに平文で書き込まれなくなる未来が来るといいですね。これに関する話題は[Storing sensitive values in state files · Issue #516 · hashicorp/terraform](https://github.com/hashicorp/terraform/issues/516)があります。Ephemeral ResourceでData SourceのStateに対する対応が入ったので、次はResourceのフィールド対応が待たれます。[^5]

# 参考

* [Data Sources - Configuration Language | Terraform | HashiCorp Developer](https://developer.hashicorp.com/terraform/language/data-sources)
* [Ephemeral resource configuration reference | Terraform | HashiCorp Developer](https://developer.hashicorp.com/terraform/language/v1.10.x/resources/ephemeral)
* [aws_ssm_parameter | Ephemeral Resources | hashicorp/aws | Terraform | Terraform Registry](https://registry.terraform.io/providers/hashicorp/aws/latest/docs/ephemeral-resources/ssm_parameter)
* [Terraform 1.10がGAになり、Ephemeral Valuesが使えるようになりました | DevelopersIO](https://dev.classmethod.jp/articles/terraform-1-10-is-now-generally-available/)


[^1]: Ephemeral variablesは、variableseに`ephemeral = true`を指定したものです。
[^2]: Ephemeral outputsは、outputsに`ephemeral = true`を指定したものです。モジュールのoutputsでのみ利用でき、root moduleのoutputsで利用できません。
[^3]: プロビジョナーやコネクションブロックを使っている人はそれほど多くないと予想しますが、使えてよかったよかった。
[^4]: DBのプロビジョニングにTerraformを使いたくないですが、用途があるのは確かにそう
[^5]: Stateファイル上で平文保持ではなく、対象フィールドに対して何かしらの暗号化が施せるようにするぐらいしか思いつかいないですが果たしてそれがベターかというと微妙そう
